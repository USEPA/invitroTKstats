#-------------------------------------  -----------------------------------------
# Name:       PPB-model.bug
# Description: A JAGS model for drawing inference of chemical-specific 
#              paramters from an in vitro assay
#
# Author:      John Wambaugh
#------------------------------------------------------------------------------
                                                   
model {

# Measurement Model:
  log.const.analytic.sd ~ dnorm(-2,0.1)
  const.analytic.sd <- 10^log.const.analytic.sd
  log.hetero.analytic.slope.factor ~ dnorm(1,.1)
  hetero.analytic.slope.factor <- 10^log.hetero.analytic.slope.factor
  hetero.analytic.slope <- hetero.analytic.slope.factor*const.analytic.sd
# Each j is a different PPB concentration measurement (samples were analyzed on different days):
  for (j in 1:3)
  {
    background[j] ~ dunif(0,1000)          
    log.calibration[j] ~ dnorm(0,.1)
    calibration[j] <- 10^log.calibration[j]
  # Concentrations below this value are not detectable:
    log.C.thresh[j] ~ dnorm(log(C.frank/2),0.25)T(log(C.frank/1000),log(10*C.frank))
    C.thresh[j] <- exp(log.C.thresh[j])
    Blank.pred[j] <- background[j]  
    Blank.prec[j] <-  (const.analytic.sd+hetero.analytic.slope*(Blank.pred[j]))^(-2)
    for (i in 1:Num.Blank.obs[j]) {
      Blank.data[j,i] ~ dnorm(Blank.pred[j],Blank.prec[j])
    }
    T0.pred[j] <- calibration[j]*(C.frank/5-C.thresh[j]) + background[j]
    T0.prec[j] <- (const.analytic.sd+hetero.analytic.slope*(T0.pred[j]))^(-2)
    for (i in 1:Num.T0.obs[j]) {
  # Must include the dilution factor (5 for Plasma)
      T0.data[j,i] ~ dnorm(T0.pred[j],T0.prec[j]) 
    }
  # Must include the dilution factor (2 for PBS)
    PBS.pred[j] <- calibration[j]*(C.u[j]/2 - C.thresh[j])*step(C.u[j]/2 - C.thresh[j]) + background[j]
    PBS.prec[j] <- (const.analytic.sd+hetero.analytic.slope*(PBS.pred[j]))^(-2)
    for (i in 1:Num.PBS.obs[j]) {
        PBS.data[j,i] ~dnorm(PBS.pred[j],PBS.prec[j])
    }
  # Must include the dilution factor (5 for Plasma)
    Plasma.pred[j] <- calibration[j]*(C.upb[j]/5 - C.thresh[j])*step(C.upb[j]/5 - C.thresh[j]) + background[j]
    Plasma.prec[j] <- (const.analytic.sd+hetero.analytic.slope*(Plasma.pred[j]))^(-2)
    for (i in 1:Num.Plasma.obs[j]) {
        Plasma.data[j,i] ~dnorm(Plasma.pred[j],Plasma.prec[j])
    }
  }
  
  
 # Binding Model:
  log.Kd ~ dunif(-10,5)
  Kd <- 10^log.Kd

  for (i in 1:3) 
  {
# Missing (bound to walls/membrane) chemical:
    C.missing[i] ~ dunif(0,C.frank)
# Unbound concentration in both wells:
    C.u[i] <- (C.frank-C.missing[i])*(Kd/(2*Kd+C.protein[i]))
# Bound concentration in plasma well:
    C.b[i] <- (C.frank-C.missing[i])*C.protein[i]/(2*Kd+C.protein[i])
# Toal concentration in plasma well:
    C.upb[i] <- C.b[i] + C.u[i]
  }
# Index 3 corresponds to 100% physiologic protein concentration:  
  Fup <- C.u[3] / C.upb[3]

  
}
